   10 REM CALCS
   20 REM Competition number calculator
   30 REM Written 27 July 1986
   40 REM Version 1.01
   50 REM Ammended 12 May 1987
   60 REM
   70 *K.0MO.7|MVDU14|ML.1590,|M
   80 *K.1RUN|M
   90 *K.2REN.|M
  100 *K.10O.|M*FX12,2|M
  110 *TV255
  120 MODE7
  130 HIMEM=&3100
  140 PROCsetup
  150 PROCassem
  160 REPEAT
  170 PROCsetua
  180 REPEAT
  190 *FX15
  200 keypr=GET
  210 UNTIL keypr>48 AND keypr<53
  220 CLS
  230 *FX15
  240 IFkeypr=49:PROCdetal
  250 IFkeypr=50:PROCloads
  260 IFkeypr=51:PROCcatal
  270 IFkeypr=52:PROCprynt
  280 UNTIL0
  290 END
  300 DEFPROCsetua
  310 CLS
  320 VDU23,1,0;0;0;0;
  330 PRINTTAB(14,2)"CALCULATOR"
  340 PRINTTAB(14,3)"=========="
  350 PRINTTAB(1,6)"1. Calculate number"
  360 PRINTTAB(1,8)"2. Load data"
  370 PRINTTAB(1,10)"3. Catalogue disc"
  380 PRINTTAB(1,12)"4. Hardcopy of NUMBER and EDIT codes"
  390 PRINTTAB(10,20)"Make your choice now"
  400 ENDPROC
  410 DEFPROCsetup
  420 DIM CODE% 256,VIEW% 700
  430 @%=0
  440 WORDS=&31E0
  450 BLOCK=&3230
  460 WARPS=&3240
  470 FISIC=&32C0
  480 BASES=&32E0
  490 ENDPROC
  500 DEFPROCloads
  510 PRINTTAB(12,2)"LOAD DATA SET"
  520 PRINTTAB(12,3)"============="
  530 PRINTTAB(4,6)"Input name of data set in"
  540 PRINTTAB(4,7)"which you wish to calculate."
  550 PRINTTAB(7,9)">=";
  560 INPUT" "files$
  570 OSCLI"*LOAD "+files$+" 31E0"
  580 CALLEXORS
  590 ENDPROC
  600 DEFPROCcatal
  610 PRINTTAB(10,2)"DISC CATALOGUE"
  620 PRINTTAB(10,3)"=============="
  630 *CAT
  640 PRINTTAB(7,20)"Press a key to continue."
  650 REPEAT
  660 UNTILINKEY(-129)<>-1
  670 ENDPROC
  680 DEFPROCdetal
  690 CLS
  700 PRINTTAB(10,2)"CALCULATE NUMBER"
  710 PRINTTAB(10,3)"================"
  720 PRINTTAB(4,6)"Please wait calculating..."
  730 S%=0
  740 FORA%=0 TO 7
  750 PROCexpan
  760 PROCscore
  770 NEXT
  780 !tempa=S%
  790 CALLHIGHR
  800 PRINTTAB(4,10)"Competition number:";
  810 PRINT?tempa+(?tempx*&100)+(?tempy*&10000)+(?tempz*&1000000)
  820 *FX15
  830 VDU7
  840 F=GET
  850 ENDPROC
  860 DEFPROCexpan
  870 !param=VIEW%
  880 param!2=(A%*420)+BASES
  890 param?4=84
  900 CALLUPACK
  910 ENDPROC
  920 DEFPROCscore
  930 FORJ%=0TO671
  940 IFJ%?VIEW%=1:S%=S%+5:NEXT:ENDPROC
  950 IFJ%?VIEW%=22:S%=S%+5:NEXT:ENDPROC
  960 IFJ%?VIEW%=23:S%=S%+5:NEXT:ENDPROC
  970 IFJ%?VIEW%=24:S%=S%+20:NEXT:ENDPROC
  980 IFJ%?VIEW%=29:S%=S%+50
  990 NEXT
 1000 ENDPROC
 1010 DEFPROCassem
 1020 pages=&60
 1030 works=&70
 1040 sorks=&80
 1050 tempa=&88
 1060 tempx=&89
 1070 tempy=&8A
 1080 tempz=&8B
 1090 indir=&8C
 1100 FORA%=0TO2 STEP2
 1110 P%=CODE%
 1120 [OPTA%
 1130 .INITL
 1140 LDX#4
 1150 .trans
 1160 LDAparam,X:STApages,X
 1170 DEX:BPLtrans:RTS
 1180 .ZEROS
 1190 LDApages:CLC:ADC#8:STApages:BCCcrate:INCpages+1
 1200 .crate LDApages+2:CLC:ADC#5:STApages+2:BCCcrabs:INCpages+3:.crabs RTS
 1210 .UPACK
 1220 JSRINITL
 1230 .mishs LDY#4:.quatr LDA(pages+2),Y:STAsorks,Y:DEY:BPLquatr:INY
 1240 .relod
 1250 LDA#5:STApages+5
 1260 .again
 1270 LDX#4
 1280 .rotat
 1290 RORsorks,X:DEX:BPLrotat
 1300 RORA:DECpages+5:BNEagain
 1310 LSRA:LSRA:LSRA:STAworks,Y:INY:CPY#8:BCCrelod
 1320 LDY#7
 1330 .strch
 1340 LDAworks,Y:STA(pages),Y:DEY:BPLstrch
 1350 JSRZEROS:DECpages+4:BNEmishs:RTS
 1360 .param
 1370 EQUD0
 1380 EQUD0
 1390 .HIGHR
 1400 LDA#WORDS AND255:STAindir
 1410 LDA#WORDS/256:STAindir+1
 1420 LDA#&FF:STAtempy
 1430 LDA#&EE:STAtempz
 1440 LDY#0
 1450 .chexs
 1460 LDA(indir),Y:EORtempy:STAtempy
 1470 EORtempz:STAtempz
 1480 INY:BNEchexs
 1490 INCindir
 1500 LDAindir:CMP#&40:BCCchexs
 1510 RTS
 1520 .EXORS
 1530 LDA#0:STAtempa:LDX#63
 1540 .exors
 1550 LDAWORDS,X:EORtempa:STAWORDS,X
 1560 INCtempa
 1570 DEX:BPLexors:RTS
 1580 ]:NEXT
 1590 ENDPROC
 1600 DEFPROCprynt
 1610 *FX6,0
 1620 CLS
 1630 INPUT"FILE NAME AND MACHINE? "U$
 1640 CLS
 1650 VDU2,1,27,1,ASC("x"),1,1
 1660 PRINT"FILE INFORMATION:- ";U$'
 1670 PRINT"SCREEN    EDIT     PASSWORD   TIME"'
 1680 FORA%=0TO15STEP2
 1690 J%=(A%DIV2)+65
 1700 PRINT"SCREEN "CHR$J%SPC2;
 1710 PRINTA%!&3230 AND&FFFF;
 1720 PRINTTAB(19);
 1730 J%=((A%DIV2)*8)+&31E0
 1740 REPEAT
 1750 PRINTCHR$(?J%);
 1760 J%=J%+1
 1770 UNTIL?J%=&D
 1780 J%=((A%DIV2)*2)+&3220
 1790 PRINTTAB(30)!J% AND&FFFF
 1800 NEXT
 1810 PRINT'"COMPETITION NUMBER:- ";
 1820 PRINT?tempa+(?tempx*&100)+(?tempy*&10000)+(?tempz*&1000000)'
 1830 VDU3
 1840 PRINTTAB(13,18)"ˆPRESS SPACE"
 1850 REPEAT:UNTILINKEY-99
 1860 ENDPROC

