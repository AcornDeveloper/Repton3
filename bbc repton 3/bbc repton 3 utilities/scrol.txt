   10 REM Use Z X * ? to scroll screen.
   20 REM If you press SHIFT also, the
   30 REM screen will scroll in sections.
   40 ONERRORRUN
   50 PROCass
   60 MODE7
   70 PRINT"Press f0 to load a new file."
   80 PRINT"Press f1 to select new screen."
   90 PRINT'"ƒREPTON 3 - Screen Scroller‡"''
  100 PRINT" Input filename: ";:INPUT""A$
  110 ONERROR REPORT:PRINT':GOTO100
  120 OSCLI("*LOAD "+A$+" 31E0")
  130 ONERROROFF
  140 PRINT''" Which screen (A-H)? ";:*FX15,1
  150 REPEATA%=GET:IFA%>96 A%=A%-32
  160 UNTILA%>64 AND A%<73:VDUA%
  170 MODE5
  180 VDU23;8202;0;0;0;
  190 A%=A%-64:PROCexpscr(A%)
  200 C%=&32C0+4*(A%-1)
  210 VDU19,0,(?C%);0;19,1,(C%?1);0;19,2,(C%?2);0;19,3,(C%?3);0;
  220 PROCdrwscr(0,0)
  230 P%=0:Q%=0
  240 REPEATPROCmovit
  250 UNTIL INKEY-33 OR INKEY-114
  260 IFINKEY-33 GOTO60
  270 MODE7:PRINT'"ƒREPTON 3 - Screen Scroller‡":GOTO140
  280 REM "END OF MAIN PROGRAM‡"
  290 DEFPROCexpscr(S%)
  300 LOCAL A%,B%,I%
  310 A%=&32E0+420*(S%-1)
  320 B%=&3020
  330 FORI%=A%TO(A%+415)STEP5
  340 B%?7=((I%?4)AND&F8)DIV8
  350 B%?6=((I%?4)AND&7)*4+((I%?3)AND&C0)DIV&40
  360 B%?5=((I%?3)AND&3E)DIV2
  370 B%?4=((I%?3)AND1)*&10+((I%?2)AND&F0)DIV&10
  380 B%?3=((I%?2)AND&F)*2+((I%?1)AND&80)DIV&80
  390 B%?2=((I%?1)AND&7C)DIV4
  400 B%?1=((I%?1)AND&3)*8+((?I%)AND&E0)DIV&20
  410 ?B%=(?I%)AND&1F
  420 B%=B%+8
  430 NEXT
  440 ENDPROC
  450 DEFPROCass
  460 LOCALI%
  470 FORI%=0TO2STEP2:P%=&2F00
  480 [OPTI%
  490 .st LDX#4
  500 .st1 LDY#&1F
  510 .st2 LDA(&72),Y:STA(&70),Y:DEY:BPLst2
  520 .st3 DEX:BEQfin
  530 CLC:LDA&70:ADC#&40:STA&70:LDA&71:ADC#1:STA&71
  540 CLC:LDA&72:ADC#&20:STA&72:BCCst1:INC&73:CLC:BCCst1
  550 .fin RTS
  560 ]:NEXT
  570 ENDPROC
  580 DEFPROCdrwscr(X%,Y%)
  590 LOCALA%,I%,J%
  600 A%=&5800
  610 FORJ%=Y%TOY%+7
  620 FORI%=X%TOX%+9
  630 !&70=A%
  640 !&72=&4000+(&80*?(&3020+J%*28+I%))
  650 CALL&2F00
  660 IFI%=X%+9 A%=A%+&3E0 ELSE A%=A%+&20
  670 NEXT,
  680 ENDPROC
  690 DEFPROCmovit
  700 LOCAL I%,J%
  710 *FX15,0
  720 I%=P%:J%=Q%
  730 IFINKEY-1=0 GOTO830
  740 IFINKEY-98=0 GOTO760
  750 IFP%>6 P%=P%-7 ELSE P%=0
  760 IFINKEY-67=0 GOTO780
  770 IFP%<12 P%=P%+6 ELSE P%=18
  780 IFINKEY-73=0 GOTO800
  790 IFQ%>5 Q%=Q%-6 ELSE Q%=0
  800 IFINKEY-105=0 GOTO870
  810 IFQ%<11 Q%=Q%+6 ELSE Q%=16
  820 GOTO870
  830 IFINKEY-98 ANDP%>0 P%=P%-1
  840 IFINKEY-67 ANDP%<18 P%=P%+1
  850 IFINKEY-73 ANDQ%>0 Q%=Q%-1
  860 IFINKEY-105 ANDQ%<16 Q%=Q%+1
  870 IF I%<>P% OR J%<>Q% PROCdrwscr(P%,Q%)
  880 ENDPROC

