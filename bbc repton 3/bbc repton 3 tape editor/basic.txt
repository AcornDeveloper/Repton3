   10 REM BASIC
   20 *K.0MO.7|MVDU14|ML.1930,|M
   30 *K.1RUN|M
   40 *K.2REN.|M
   50 *K.3P.~P%|M
   60 *K.4*S."T.REPTON3" 3000 +400 400 400|M
   70 *K.10PA.=&5900|MO.|M*FX12,2|M
   80 *TV255
   90 diary=&40
  100 store=&46
  110 aaaaa=&50
  120 xxxxx=&51
  130 yyyyy=&52
  140 vectr=&53
  150 scren=&55
  160 pages=&60
  170 tempa=&70
  180 tempx=&71
  190 tempy=&72
  200 tempz=&73
  210 trubl=&9C
  220 teeth=&9E
  230 STACK=&100
  240 OSWVC=&20E
  250 VIDEO=&300
  260 WRITE=&1500
  270 WRONG=&1503
  280 INDIR=&1556
  290 WIPES=&155F
  300 NOISE=&1565
  310 METAL=&1568
  320 grids=&1585
  330 TI320=&1A80
  340 STUFF=&260C
  350 SMALL=&290F
  360 WORDS=&31E0
  370 CODES=&3230
  380 himem=&5800
  390 below=&5FB8
  400 botof=&7100
  410 OSO=&FFF1
  420 OSB=&FFF4
  430 OSC=&FFF7
  440 FORA%=4TO6STEP2
  450 P%=&400
  460 O%=&3000
  470 [OPTA%
  480 .STRIN JMPA
  490 .ALTER JMPB
  500 .MOTTL JMPC
  510 .PEEPS JMPD
  520 .EXORS JMPE
  530 .EDITS EQUD0:EQUD0
  540 .washs
  550 EQUB12
  560 EQUW&211
  570 EQUW&31F:EQUB19
  580 EQUS"(c) Superior Software LTD."
  590 EQUW&D1F:EQUB21
  600 EQUS"1986"
  610 EQUW&311
  620 EQUW&81F:EQUB12
  630 EQUS"Written by"
  640 EQUW&81F:EQUB14
  650 EQUS"Matthew Atkinson"
  660 EQUB26
  670 EQUW&211:EQUW&8011
  680 EQUW&C1F:EQUB5
  690 EQUS"REPTON 3"
  700 EQUW&B1F:EQUB7
  710 EQUS"THE EDITOR"
  720 .A
  730 LDAOSWVC:STAvectr
  740 LDAOSWVC+1:STAvectr+1
  750 LDA#PATCH AND255:STAOSWVC
  760 LDA#PATCH/256:STAOSWVC+1
  770 LDX#washs AND255:LDY#washs/256
  780 LDA#A-washs:JMPINDIR
  790 .B
  800 RTS
  810 .cover
  820 EQUB18:EQUB0:EQUB1
  830 EQUW&419:EQUW0:EQUW0
  840 EQUW&519:EQUW1280:EQUW0
  850 EQUW&419:EQUW64:EQUW220
  860 EQUW&119:EQUW896:EQUW0
  870 .C
  880 LDA#6:STApages
  890 .lines
  900 LDA#32:STApages+1
  910 .cipes
  920 LDApages+1:CLC:ADC#3:TAX
  930 LDApages:CLC:ADC#25:TAY
  940 LDA#48:JSRSMALL
  950 DECpages+1:BPLcipes
  960 DECpages:BPLlines
  970 LDX#cover AND255:LDY#cover/256
  980 LDA#C-cover:JMPINDIR
  990 .E
 1000 LDA#0:STAtempa:LDX#63
 1010 .exors
 1020 LDAWORDS,X:EORtempa:STAWORDS,X
 1030 INCtempa
 1040 DEX:BPLexors
 1050 .dogit RTS
 1060 .peeps
 1070 EQUW&11:EQUW&8211
 1080 EQUB28
 1090 EQUB2:EQUB23
 1100 EQUB13:EQUB2
 1110 EQUB12:EQUB26
 1120 EQUW&B1F:EQUB3
 1130 EQUS"ENTER CODES"
 1140 EQUW&B1F:EQUB4
 1150 EQUS"==========="
 1160 .D
 1170 LDA#0:LDX#7
 1180 .clear
 1190 STAEDITS,X:DEX:BPLclear
 1200 LDACODES:ORACODES+1
 1210 BEQdogit
 1220 LDA#124:JSROSB
 1230 LDX#peeps AND255:LDY#peeps/256
 1240 LDA#D-peeps:JSRINDIR
 1250 LDA#15:LDX#0:JSROSB
 1260 LDA#0:STApages
 1270 .loops
 1280 LDApages:ASLA:ADC#6
 1290 STAbegin:STApassd
 1300 LDApages:CLC:ADC#65:STApword
 1310 LDX#stray AND255:LDY#stray/256
 1320 LDA#bytes-stray:JSRINDIR
 1330 LDX#param AND255:LDY#param/256
 1340 LDA#0:JSROSO:BCSabort
 1350 JSRCONVR
 1360 LDApages:ASLA:TAX:TAY
 1370 LDAdiary:CMP#&D:BEQnorec
 1380 LDAtempa:CMPCODES,X:BNEnorec
 1390 LDAtempx:CMPCODES+1,X:BNEnorec
 1400 .strin
 1410 LDXpages:LDA#0:STAEDITS,X
 1420 LDX#coret AND255:LDY#coret/256
 1430 LDA#space-coret:JSRINDIR
 1440 JSRNOISE:JMPloopx
 1450 .norec
 1460 LDXpages:BNEkilit
 1470 JMPPEEPS
 1480 .kilit
 1490 LDA#1:STAEDITS,X
 1500 .loopx
 1510 INCpages:LDApages:CMP#8:BCCloops
 1520 LDX#space AND255:LDY#space/256
 1530 LDA#param-space:JSRINDIR
 1540 .sorts
 1550 LDX#&9D:JSRMETAL:BPLsorts
 1560 JMPWIPES
 1570 .abort
 1580 JSRWIPES
 1590 JMPSTUFF
 1600 .coret
 1610 EQUW&111F
 1620 .passd
 1630 EQUB0
 1640 EQUS"Correct!"
 1650 .space
 1660 EQUW&61F:EQUB22
 1670 EQUS"Press <SPACE> to EXIT"
 1680 .param
 1690 EQUWdiary:EQUB5:EQUB32:EQUB128
 1700 .stray
 1710 EQUW&81F
 1720 .begin
 1730 EQUB0
 1740 .pword
 1750 EQUS" =>"
 1760 .bytes
 1770 EQUD&100
 1780 EQUD&0
 1790 EQUD&0
 1800 EQUD&0
 1810 EQUD&5028100
 1820 EQUD&9010000
 1830 EQUD&508
 1840 EQUD&82000404
 1850 .flaps EQUB0
 1860 .count EQUB0
 1870 .addrs EQUWhimem
 1880 .masks EQUW0
 1890 .works EQUB0
 1900 .lefty
 1910 LDAscren:STAaddrs
 1920 LDAscren+1:STAaddrs+1
 1930 JMPwings
 1940 .downs
 1950 LDAscren:CLC:ADC#&40
 1960 STAscren:STAaddrs
 1970 LDAscren+1:ADC#1
 1980 STAscren+1:STAaddrs+1
 1990 LDA#botof AND255:SEC:SBCscren
 2000 LDA#botof/256:SBCscren+1
 2010 BCSloada
 2020 LDA#below AND255:STAscren:STAaddrs
 2030 LDA#below/256
 2040 STAscren+1:STAaddrs+1
 2050 .loada
 2060 LDAaaaaa:RTS
 2070 .PATCH
 2080 STAaaaaa:STXxxxxx:STYyyyyy
 2090 LDAflaps:BNEtraps
 2100 LDAcount:BNEnzero
 2110 LDAaaaaa:CMP#32:BCClocxs
 2120 JMPchars
 2130 .locxs
 2140 CMP#&D:BEQlefty
 2150 CMP#10:BEQdowns
 2160 TAY
 2170 LDAbytes,Y:AND#&F:STAcount
 2180 LDAbytes,Y:BPLwings:STYflaps
 2190 .wings
 2200 LDAaaaaa:LDXxxxxx:LDYyyyyy
 2210 JMP(vectr)
 2220 .nzero
 2230 DECcount:JMPwings
 2240 .traps
 2250 LDXcount:LDAaaaaa:STAstore-1,X
 2260 DECcount:BNEwings
 2270 LDAflaps:CMP#31:BNEcotin
 2280 LDAstore:ASLA:TAX
 2290 LDAstore+1:ASLA:ASLA:ASLA
 2300 ROLaddrs+1:CLC:ADCTI320,X
 2310 STAaddrs
 2320 LDAaddrs+1:AND#1:ADCTI320+1,X
 2330 STAaddrs+1
 2340 .comms
 2350 LDA#0:STAflaps
 2360 LDA#below AND255:STAscren
 2370 LDA#below/256:STAscren+1
 2380 JMPwings
 2390 .cotin
 2400 LDAstore:ROLA:ROLA:AND#1
 2410 TAX:LDAstore:AND#&F:STAmasks,X
 2420 JMPcomms
 2430 .chars
 2440 SEC:SBC#32:PHA
 2450 CMP#95:BNEnodel
 2460 LDAaddrs:SEC:SBC#8:STAaddrs
 2470 LDAaddrs+1:SBC#0
 2480 CMP#himem/256:BCSlower:LDA#&80
 2490 .lower
 2500 STAaddrs+1:LDA#0
 2510 .nodel
 2520 ASLA:ROLtrubl+1
 2530 ASLA:ROLtrubl+1
 2540 ASLA:ROLtrubl+1:STAtrubl:CLC
 2550 LDAtrubl+1:AND#7:ADC#&12
 2560 STAtrubl+1
 2570 LDAaddrs:STAteeth
 2580 LDAaddrs+1:STAteeth+1
 2590 LDY#7
 2600 .celit
 2610 LDA(trubl),Y
 2620 LSRA:LSRA:LSRA:LSRA:TAX:PHA
 2630 LDAtable,X
 2640 LDXmasks:ANDsmall,X:STAworks
 2650 PLA:EOR#&F:TAX
 2660 LDAtable,X
 2670 LDXmasks+1:ANDsmall,X
 2680 ORAworks
 2690 STA(teeth),Y
 2700 DEY:BPLcelit
 2710 PLA:CMP#95:BEQdelet
 2720 LDAaddrs:CLC:ADC#8:STAaddrs
 2730 LDAaddrs+1:ADC#0:BPLwraps
 2740 LDA#himem/256
 2750 .wraps
 2760 STAaddrs+1
 2770 .delet
 2780 LDAaaaaa:LDXxxxxx:LDYyyyyy:RTS
 2790 .table
 2800 EQUD&33221100
 2810 EQUD&77665544
 2820 EQUD&BBAA9988
 2830 EQUD&FFEEDDCC
 2840 .small
 2850 EQUD&FFF00F00
 2860 .CONVR
 2870 LDY#0
 2880 LDX#0:STXtempa:STXtempx:BEQregin
 2890 .subbo
 2900 STAtempy:STXtempz
 2910 ASLtempa:ROLtempx
 2920 LDAtempy:ADCtempa:STAtempy
 2930 LDAtempz:ADCtempx:STAtempz
 2940 ASLtempa:ROLtempx
 2950 ASLtempa:ROLtempx
 2960 LDAtempa:ADCtempy:STAtempa
 2970 LDAtempx:ADCtempz:STAtempx
 2980 .regin LDAdiary,Y:INY
 2990 SEC:SBC#&30:CMP#&A:BCCsubbo:RTS
 3000 ]:NEXT

