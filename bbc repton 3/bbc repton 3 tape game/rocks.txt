   10 REM ROCKS
   20 *K.0MO.7|MVDU14|ML.3680,|M
   30 *K.1RUN|M
   40 *K.2REN.|M
   50 *K.3P.~P%|M
   60 *K.10PA.=&5900|MO.|M*FX12,2|M
   70 *TV255
   80 userp=0
   90 monst=&2
  100 index=&8
  110 xcoor=&10
  120 ycoor=&14
  130 anoth=&24
  140 quart=&6C
  150 kicks=&6F
  160 pages=&60
  170 tempa=&70
  180 tempx=&71
  190 tempy=&72
  200 tempz=&73
  210 indir=&74
  220 tunes=&76
  230 death=&78
  240 worka=&7A
  250 score=&80
  260 tiara=&82
  270 paras=&87
  280 repty=&96
  290 MONST=&407
  300 BARS1=&735
  310 BARS2=&757
  320 BARS3=&777
  330 BARS4=&797
  340 BARS5=&735
  350 BARS6=&757
  360 BARS7=&7B7
  370 BARS8=&7D7
  380 TYLER=&1503
  390 INDIR=&1509
  400 DECIM=&1700
  410 NOISE=&1703
  420 METAL=&1706
  430 PASTL=&1715
  440 COLOF=&1913
  450 ROTAT=&1919
  460 SIGNS=&191C
  470 DELAY=&1CA3
  480 GBYTE=&1CA6
  490 WBYTE=&1CA9
  500 BLOBS=&2000
  510 PLACE=&2003
  520 OPENS=&27A3
  530 WORDS=&31E0
  540 SHELA=&FE00
  550 OSW=&FFEE
  560 OSO=&FFF1
  570 OSB=&FFF4
  580 FORA%=0TO2STEP2
  590 P%=&2960
  600 [OPTA%
  610 .ROCKS JMPA
  620 .CONGR JMPB
  630 .MUSIC JMPC
  640 .DIABL JMPD
  650 .ENABL JMPE
  660 .CLOCK JMPF
  670 .FINAL JMPG
  680 .A
  690 LDX#0:STXpages+4
  700 LDArepty:STApages+1
  710 JSRlines
  720 LDYrepty:DEY:DEY:STYpages+1
  730 JSRlines
  740 LDAkicks:BNEDUMMY
  750 LDYrepty:DEY:STYpages+1
  760 JSRlines
  770 DECquart:LDAquart:AND#3:TAX
  780 LDApiece,X:STApages+1
  790 LDX#5:STXpages+4
  800 .lines
  810 LDA#27:STApages
  820 .cross
  830 LDApages+1:LDXpages:JSRGBYTE
  840 BEQdrops
  850 CMP#24:BNEbashm
  860 .drops
  870 STAworka:JSRCLOBR
  880 LDXpages:LDYpages+1
  890 INY:TYA:JSRGBYTE
  900 TAX:LDAreson,X:ASLA:TAX
  910 LDA#resum/256:PHA
  920 LDA#(resum AND255)-1:PHA
  930 LDApolyc+1,X:PHA
  940 LDApolyc,X:PHA
  950 .DUMMY RTS
  960 .resum
  970 JSRCLOBR
  980 .bashm
  990 DECpages:BPLcross
 1000 INCpages+1
 1010 LDYrepty:CPYpages+1
 1020 BNErocks:INCpages+1
 1030 .rocks
 1040 DECpages+4:BPLlines
 1050 .retun RTS
 1060 .piece
 1070 EQUB0:EQUB6:EQUB12:EQUB18
 1080 .polyc
 1090 EQUWDUMMY-1:EQUWFALIN-1
 1100 EQUWLANDR-1:EQUWFLEFT-1
 1110 EQUWFRIGH-1
 1120 .reson
 1130 EQUB2:EQUB2:EQUB0:EQUB0
 1140 EQUB2:EQUB2:EQUB1:EQUB0
 1150 EQUB0:EQUB0:EQUB0:EQUB0
 1160 EQUB3:EQUB4:EQUB0:EQUB0
 1170 EQUB0:EQUB3:EQUB4:EQUB0
 1180 EQUB0:EQUB0:EQUB0:EQUB0
 1190 EQUB2:EQUB2:EQUB0:EQUB2
 1200 EQUB0:EQUB2:EQUB0:EQUB0
 1210 EQUB0:EQUB0:EQUB0:EQUB0
 1220 EQUB0:EQUB0:EQUB0:EQUB0
 1230 EQUB0:EQUB0:EQUB0:EQUB0
 1240 EQUB0:EQUB0:EQUB0:EQUB0
 1250 .FALIN
 1260 JSRBOULD
 1270 LDXpages:LDYpages+1
 1280 INY:JSRSTONE
 1290 LDXpages:LDYpages+1:INY:INY:TYA
 1300 JSRGBYTE:CMP#30:BEQbonks
 1310 RTS
 1320 .LANDR
 1330 LDAkicks:BNEnears
 1340 LDXpages:DEX:LDApages+1:JSRGBYTE
 1350 CMP#6:BNEFRIGH:LDXpages:DEX
 1360 LDYpages+1:INY:TYA:JSRGBYTE
 1370 CMP#6:BNEFRIGH
 1380 JSRBOULD
 1390 LDXpages:DEX
 1400 LDYpages+1:INY:JSRSTONE
 1410 LDXpages:DEX:LDYpages+1
 1420 INY:INY:TYA
 1430 JSRGBYTE:CMP#30:BEQbonks
 1440 INCpages+1:INCpages+1:RTS
 1450 .bonks INCdeath:.nears RTS
 1460 .FLEFT
 1470 LDXpages:DEX:LDApages+1:JSRGBYTE
 1480 CMP#6:BNEnears:LDXpages:DEX
 1490 LDYpages+1:INY:TYA:JSRGBYTE
 1500 CMP#6:BNEnears
 1510 JSRBOULD
 1520 LDXpages:DEX
 1530 LDYpages+1:INY:JSRSTONE
 1540 LDXpages:DEX:LDYpages+1
 1550 INY:INY:TYA
 1560 JSRGBYTE:CMP#30:BEQbonks
 1570 INCpages+1:INCpages+1:RTS
 1580 .FRIGH
 1590 LDXpages:INX:LDApages+1:JSRGBYTE
 1600 CMP#6:BNEnears:LDXpages:INX
 1610 LDYpages+1:INY:TYA:JSRGBYTE
 1620 CMP#6:BNEnears
 1630 JSRBOULD
 1640 LDXpages:INX
 1650 LDYpages+1:INY:JSRSTONE
 1660 LDXpages:INX:LDYpages+1
 1670 INY:INY:TYA
 1680 JSRGBYTE:CMP#30:BEQbonks
 1690 INCpages+1:INCpages+1
 1700 .nifes RTS
 1710 .STONE
 1720 STXpages+2:STYpages+3
 1730 LDAworka:STAparas:JSRWBYTE
 1740 LDApages+2:STAtempa
 1750 LDApages+3:STAtempy
 1760 LDA#0:STAtempx:STAtempz
 1770 JSROPENS:BCShatch
 1780 JSRPLACE
 1790 LDA#6:STAparas
 1800 JSRPLACE
 1810 .hatch
 1820 LDAworka:CMP#24:BNEnifes
 1830 LDXpages+2:LDYpages+3
 1840 INY:TYA:JSRGBYTE
 1850 CMP#6:BEQnifes
 1860 LDA#47:STAparas
 1870 LDXpages+2:LDYpages+3:JSRWBYTE
 1880 LDApages+2:STAtempa
 1890 LDApages+3:STAtempy
 1900 LDA#0:STAtempx:STAtempz
 1910 JSROPENS
 1920 BCSdepth:JSRPLACE
 1930 LDA#24:STAparas:JSRPLACE
 1940 .depth
 1950 LDX#3
 1960 .lucid
 1970 LDAanoth,X:BPLstein
 1980 LDA#32:STAanoth,X
 1990 LDApages+2:STAxcoor,X
 2000 LDApages+3:STAycoor,X
 2010 LDA#4:JMPNOISE
 2020 .stein
 2030 DEX:BPLlucid:RTS
 2040 .BOULD
 2050 LDA#1:JSRNOISE
 2060 LDA#6:STAparas:LDXpages
 2070 LDYpages+1:JSRWBYTE
 2080 LDApages:STAtempa
 2090 LDApages+1:STAtempy
 2100 LDA#0:STAtempx:STAtempz
 2110 JSROPENS
 2120 BCSother:JSRPLACE
 2130 LDAworka:STAparas
 2140 JMPPLACE
 2150 .CLOBR
 2160 LDX#3
 2170 .clobr
 2180 LDAanoth,X:BNEnomos
 2190 LDAxcoor,X:CMPpages:BNEnomos
 2200 LDAycoor,X:CMPpages+1:BNEnomos
 2210 LDA#20:CLC:ADCscore:STAscore
 2220 LDA#0:ADCscore+1:STAscore+1
 2230 DECmonst
 2240 TXA:PHA
 2250 LDA#5:JSRNOISE
 2260 JSRMONST
 2270 PLA:PHA:TAX:DECanoth,X
 2280 JSRMONST:PLA:TAX
 2290 .nomos
 2300 DEX:BPLclobr
 2310 .other RTS
 2320 .finis
 2330 EQUW&11F:EQUB14
 2340 EQUS"Amazing,you have completed all"
 2350 EQUW&11F:EQUB16
 2360 EQUS"the screens."
 2370 EQUW&11F:EQUB18
 2380 EQUS"To enter the competition,make a"
 2390 EQUW&11F:EQUB20
 2400 EQUS"note of the number below and"
 2410 EQUW&11F:EQUB22
 2420 EQUS"which set of screens you used."
 2430 EQUW&211
 2440 EQUW&71F:EQUB25
 2450 EQUS"COMPETITION NUMBER"
 2460 EQUW&B1F:EQUB27
 2470 .nofin
 2480 EQUW&31F:EQUB14
 2490 EQUS"Now try and do that from the"
 2500 EQUW&21F:EQUB16
 2510 EQUS"start without using any of the"
 2520 EQUW&31F:EQUB18
 2530 EQUS"passwords and picking up all"
 2540 EQUW&81F:EQUB20
 2550 EQUS"the eight crowns."
 2560 .retun
 2570 EQUW&111
 2580 EQUW&51F:EQUB30
 2590 EQUS"Press       to continue"
 2600 EQUW&61F:EQUB11
 2610 EQUS"HAS BEEN COMPLETED ''"
 2620 EQUW&211
 2630 EQUW&B1F:EQUB30
 2640 EQUS"SPACE"
 2650 EQUW&311
 2660 .B
 2670 JSRTYLER
 2680 JSRCOLOF
 2690 JSRSIGNS
 2700 LDX#retun AND255:LDY#retun/256
 2710 LDA#B-retun:JSRINDIR
 2720 LDAtiara:CMP#8:BCCnonum
 2730 LDAuserp:BNEnonum
 2740 LDX#finis AND255:LDY#finis/256
 2750 LDA#nofin-finis:JSRINDIR
 2760 LDAscore:STAtempa
 2770 LDAscore+1:STAtempx
 2780 LDA#WORDS AND255:STAindir
 2790 LDA#WORDS/256:STAindir+1
 2800 LDA#&FF:STAtempy
 2810 LDA#&EE:STAtempz
 2820 LDY#0
 2830 .chexs
 2840 LDA(indir),Y:EORtempy:STAtempy
 2850 EORtempz:STAtempz
 2860 INY:BNEchexs
 2870 INCindir
 2880 LDAindir:CMP#&40:BCCchexs
 2890 LDX#0
 2900 .volum
 2910 LDY#32:LDA#0
 2920 .shape
 2930 ASLtempa:ROLtempx
 2940 ROLtempy:ROLtempz
 2950 ROLA:CMP#10:BCCslipy
 2960 SBC#10:INCtempa
 2970 .slipy DEY:BNEshape
 2980 PHA:INX:LDAtempa:ORAtempx
 2990 ORAtempy:ORAtempz
 3000 BNEvolum
 3010 .words
 3020 PLA:CLC:ADC#48:JSROSW:DEX:BNEwords
 3030 JMPwaits
 3040 .nonum
 3050 LDX#nofin AND255:LDY#nofin/256
 3060 LDA#retun-nofin:JSRINDIR
 3070 .waits
 3080 JSRPASTL
 3090 .press
 3100 LDX#&9D:JSRMETAL:BPLpress
 3110 JMPROTAT
 3120 .barit EQUB0
 3130 .barrs EQUB0
 3140 .notes EQUB0
 3150 .black
 3160 EQUWBARS1
 3170 EQUWBARS2
 3180 EQUWBARS3
 3190 EQUWBARS4
 3200 EQUWBARS5
 3210 EQUWBARS6
 3220 EQUWBARS7
 3230 EQUWBARS8
 3240 .C
 3250 LDAbarit:BNEdiabl
 3260 LDAtunes:BNEsilen
 3270 LDAbarrs:ASLA:TAX
 3280 LDAblack,X:STAindex
 3290 LDAblack+1,X:STAindex+1
 3300 LDYnotes:LDA(index),Y
 3310 JSRDECOD:STAchan2+4
 3320 LDAnotes:CLC:ADC#16:TAY
 3330 LDA(index),Y
 3340 JSRDECOD:STAchan3+4
 3350 LDAchan2+4:CMP#&FF:BEQresto
 3360 LDX#chan2 AND255:LDY#chan2/256
 3370 LDA#7:JSROSO
 3380 .resto
 3390 LDAchan3+4:CMP#&FF:BEQsilen
 3400 LDX#chan3 AND255:LDY#chan3/256
 3410 LDA#7:JSROSO
 3420 .silen
 3430 INCnotes:LDAnotes
 3440 CMP#16:BCCdiabl
 3450 LDA#0:STAnotes
 3460 INCbarrs:LDAbarrs
 3470 AND#7:STAbarrs
 3480 .diabl RTS
 3490 .D
 3500 LDA#1:STAbarit:RTS
 3510 .E
 3520 LDA#0:STAbarit:RTS
 3530 .DECOD
 3540 PHA:LSRA:LSRA:LSRA:LSRA:TAX
 3550 PLA:AND#&F:TAY:LDAoctav,Y
 3560 CMP#&FF:BEQdiabl
 3570 CPX#0:BEQdiabl
 3580 .addup
 3590 CLC:ADC#48:DEX:BNEaddup:RTS
 3600 .chan2
 3610 EQUW&12:EQUW2:EQUW0:EQUW1
 3620 .chan3
 3630 EQUW&13:EQUW2:EQUW0:EQUW1
 3640 .octav
 3650 EQUB49:EQUB45:EQUB41:EQUB37
 3660 EQUB33:EQUB29:EQUB25:EQUB21
 3670 EQUB17:EQUB13:EQUB9:EQUB5
 3680 EQUB&FF:EQUB&FF:EQUB&FF:EQUB&FF
 3690 .F
 3700 LDA#&E:STASHELA+&66
 3710 LDA#&27:STASHELA+&67
 3720 STASHELA+&65
 3730 LDA#&C0:STASHELA+&6E
 3740 RTS
 3750 .final
 3760 EQUW&111
 3770 EQUW&91F:EQUB15
 3780 EQUS"SCORE :"
 3790 .G
 3800 JSRTYLER
 3810 JSRPASTL
 3820 LDX#final AND255:LDY#final/256
 3830 LDA#G-final:JSRINDIR
 3840 LDAscore:STAtempa
 3850 LDAscore+1:STAtempx
 3860 LDX#17:LDY#15:JSRDECIM
 3870 LDX#100:JSRDELAY
 3880 JMPROTAT
 3890 ]:NEXT

