   10 REM NOISE
   20 *K.0MO.7|MVDU14|ML.1200,|M
   30 *K.1RUN|M
   40 *K.2REN.|M
   50 *K.3P.~P%|M
   60 *K.10PA.=&5900|MO.|M*FX12,2|M
   70 *TV255
   80 ENVELOPE1,1,0,0,0,0,0,0,126,-1,0,-1,126,0
   90 ENVELOPE3,3,1,1,0,5,10,40,30,-10,-10,-15,127,0
  100 ENVELOPE4,131,0,0,0,25,2,-2,110,0,-4,-8,110,80
  110 pages=&60
  120 works=&66
  130 tempa=&70
  140 tempx=&71
  150 tempy=&72
  160 tempz=&73
  170 gates=&77
  180 death=&78
  190 grids=&79
  200 watch=&7D
  210 sorks=&B0
  220 FISIC=&32C0
  230 SHELA=&FE00
  240 OSW=&FFEE
  250 OSO=&FFF1
  260 OSB=&FFF4
  270 FORA%=0TO2STEP2
  280 P%=&1700
  290 [OPTA%
  300 .DECIM JMPA
  310 .NOISE JMPB
  320 .METAL JMPC
  330 .UPACK JMPD
  340 .GRIDS JMPE
  350 .MCODE JMPF
  360 .PAUSE JMPG
  370 .PASTL JMPH
  380 .TDOWN JMPI
  390 .SPACE JMPJ
  400 .color EQUB0:EQUB4:EQUB3:EQUB2
  410 .A
  420 LDA#31:JSROSW
  430 TXA:JSROSW:TYA:JSROSW
  440 LDA#17:JSROSW:LDA#2:JSROSW:LDX#0
  450 .volum
  460 LDY#16:LDA#0
  470 .shape
  480 ASLtempa:ROLtempx
  490 ROLA:CMP#10:BCCslipy:SBC#10:INCtempa:.slipy DEY:BNEshape
  500 PHA:INX:LDAtempa:ORAtempx:BNEvolum
  510 .words
  520 PLA:CLC:ADC#48:JSROSW:DEX:BNEwords
  530 .silen RTS
  540 .C
  550 LDA#129:TAY:JSROSB:TYA:RTS
  560 .noise
  570 EQUW&11:EQUW1:EQUW200:EQUW20
  580 EQUW&10:EQUW-8:EQUW4:EQUW7
  590 EQUW&10:EQUW-10:EQUW0:EQUW4
  600 EQUW&11:EQUW1:EQUW100:EQUW10
  610 EQUW&10:EQUW-15:EQUW2:EQUW3
  620 EQUW&10:EQUW-12:EQUW6:EQUW3
  630 EQUW&11:EQUW3:EQUW50:EQUW15
  640 EQUW&10:EQUW4:EQUW5:EQUW4
  650 EQUW&10:EQUW-15:EQUW1:EQUW3
  660 EQUW&11:EQUW-15:EQUW80:EQUW2
  670 EQUW&10:EQUW-12:EQUW0:EQUW4
  680 .B
  690 LDXgates:BNEsilen
  700 LDY#noise/256:ASLA:ASLA:ASLA:ADC#noise AND255:TAX
  710 LDA#7:JMP(&20C)
  720 .D
  730 STXtempa:STYtempx:LDX#4:LDY#4
  740 .trans LDA(tempa),Y:STApages,X:DEY:DEX:BPLtrans
  750 .mishs LDY#4:.quatr LDA(pages+2),Y:STAsorks,Y:DEY:BPLquatr:INY
  760 .relod LDA#5:STApages+5
  770 .again LDX#4
  780 .rotat RORsorks,X:DEX:BPLrotat
  790 RORA:DECpages+5:BNEagain
  800 LSRA:LSRA:LSRA:STAworks,Y:INY:CPY#8:BCCrelod
  810 LDY#7:.strch LDAworks,Y:STA(pages),Y:DEY:BPLstrch
  820 LDApages:CLC:ADC#8:STApages:BCCcrate:INCpages+1
  830 .crate LDApages+2:CLC:ADC#5:STApages+2:BCCcrabs:INCpages+3:.crabs
  840 DECpages+4:BNEmishs:RTS
  850 .E
  860 LDAgrids:ASLA:ASLA:TAX:LDY#0
  870 .getit
  880 LDAFISIC,X:STAcolor,Y:INX:INY:CPY#4:BCCgetit
  890 .COLOR
  900 LDA#19:JSROSB:LDX#3
  910 .phyic
  920 LDA#19:JSROSW:TXA:JSROSW
  930 LDAcolor,X:JSROSW:LDA#0:JSROSW:JSROSW:JSROSW
  940 DEX:BPLphyic:RTS
  950 .NAMES EQUS"REPTON ":EQUB&D
  960 .F
  970 STANAMES+6
  980 LDX#NAMES AND255:LDY#NAMES/256
  990 LDA#4:JMP(&21E)
 1000 .G
 1010 LDX#&8F:JSRMETAL:BPLdeads
 1020 INCdeath
 1030 .deads RTS
 1040 .logic
 1050 EQUB0:EQUB4:EQUB3:EQUB2
 1060 .H
 1070 LDX#3
 1080 .pastl
 1090 LDAlogic,X:STAcolor,X
 1100 DEX:BPLpastl:JMPCOLOR
 1110 .ticks EQUB9
 1120 .toggl EQUB0
 1130 .I
 1140 LDAwatch+1:BNEdeads
 1150 DECticks:BNEdeads
 1160 LDA#9:STAticks
 1170 INCtoggl
 1180 LDY#7
 1190 LDAtoggl:AND#1:BNEselet
 1200 LDAgrids:ASLA:ASLA:TAX
 1210 LDYFISIC,X
 1220 .selet
 1230 STYfasts+1
 1240 LDA#19:JSROSB
 1250 LDX#fasts AND255:LDY#fasts/256
 1260 LDA#&C:JSROSO
 1270 LDAtoggl:AND#1:BEQdeads
 1280 LDA#10:JMPNOISE
 1290 .fasts
 1300 EQUD0:EQUB0
 1310 .J
 1320 LDX#&9D:JSRMETAL:BMIkeyin
 1330 LDA#128:LDX#0:JSROSB
 1340 TXA:AND#1:BNEkeyin
 1350 .minus
 1360 LDY#&FF:RTS
 1370 .keyin
 1380 LDY#2:RTS
 1390 ]:NEXT

