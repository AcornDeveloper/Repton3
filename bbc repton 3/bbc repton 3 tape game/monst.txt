   10 REM MONST
   20 *K.0MO.7|MVDU14|ML.1030,|M
   30 *K.1RUN|M
   40 *K.2REN.|M
   50 *K.3P.~P%|M
   60 *K.4*S."REPTON3" 3000 +400 400 400|M
   70 *K.10PA.=&4000|MO.|M*FX12,2|M
   80 *TV255
   90 monst=&2
  100 randy=&F
  110 xcoor=&10
  120 ycoor=&14
  130 framc=&18
  140 direc=&1C
  150 monsc=&20
  160 anoth=&24
  170 monsf=&28
  180 bornm=&2C
  190 spirp=&30
  200 spirx=&38
  210 spiry=&40
  220 spirc=&48
  230 spirf=&50
  240 spird=&58
  250 pages=&60
  260 aloof=&6E
  270 tempa=&70
  280 tempx=&71
  290 tempy=&72
  300 tempz=&73
  310 tunes=&76
  320 death=&78
  330 score=&80
  340 paras=&87
  350 movie=&90
  360 reptx=&95
  370 repty=&96
  380 sting=&97
  390 count=&B0
  400 ADJUS=&CB8
  410 NOISE=&1703
  420 GBYTE=&1CA6
  430 WBYTE=&1CA9
  440 BLOBS=&2000
  450 PLACE=&2003
  460 FINDB=&2403
  470 OPENS=&27A3
  480 ROCKS=&2960
  490 OSO=&FFF1
  500 OSB=&FFF4
  510 oct2=0
  520 oct3=&10
  530 oct4=&20
  540 b=0
  550 a`=1
  560 a=2
  570 g`=3
  580 g=4
  590 f`=5
  600 f=6
  610 e=7
  620 d`=8
  630 d=9
  640 c`=10
  650 c=11
  660 FORA%=4TO6STEP2
  670 P%=&400
  680 O%=&3000
  690 [OPTA%
  700 RTS
  710 .HATCH JMPA
  720 .YOLKS JMPB
  730 .MONST JMPC
  740 .CHASE JMPD
  750 .SPIRT JMPE
  760 .JINGL JMPF
  770 .A
  780 LDX#3
  790 .chick
  800 LDAanoth,X:BMIenabl:BEQenabl
  810 DECanoth,X:BNEenabl
  820 LDAmovie:CMP#4:BCCcrack
  830 STXaloof
  840 LDYycoor,X:LDAxcoor,X:TAX
  850 LDA#6:STAparas:JSRWBYTE
  860 LDXaloof
  870 LDA#32:STAbornm,X
  880 LDAxcoor,X:STAtempa
  890 LDAycoor,X:STAtempy
  900 LDA#0
  910 STAtempx:STAtempz
  920 STAmonsc,X:STAdirec,X
  930 JSROPENS:BCSeggon
  940 JSRBLOBS
  950 LDXaloof
  960 LDAframc,X:STAparas
  970 JSRPLACE
  980 JMPROCKS
  990 .crack
 1000 INCanoth,X
 1010 .enabl DEX:BPLchick
 1020 .eggon RTS
 1030 .B
 1040 LDX#3
 1050 .yolks
 1060 LDA#3:STAmonsf,X
 1070 LDA#33:STAframc,X
 1080 LDA#&FF:STAanoth,X
 1090 DEX:BPLyolks
 1100 LDX#7
 1110 .spirt
 1120 STXaloof
 1130 LDY#&FF:STYspirp,X
 1140 INY:STYspirc,X
 1150 INY:STYspird,X
 1160 LDA#3:STAcount,X
 1170 LDA#31:STAspirf,X
 1180 JSRFINDB:BCCwhisk
 1190 TXA:LDXaloof:STAspirx,X
 1200 STYspiry,X:INCspirp,X
 1210 TAX:LDA#6:JSRWBYTE
 1220 JSRADJUS
 1230 .whisk
 1240 LDXaloof
 1250 DEX:BPLspirt:RTS
 1260 .C
 1270 LDX#3
 1280 .moors
 1290 LDAanoth,X:BNEnotri:STXaloof
 1300 LDAframc,X:STAparas
 1310 LDAxcoor,X:STAtempa
 1320 LDAmonsc,X:STAtempx
 1330 LDAycoor,X:STAtempy
 1340 LDAdirec,X:STAtempz
 1350 JSROPENS:BCSleafs:JSRPLACE
 1360 .leafs LDXaloof
 1370 .notri DEX:BPLmoors:RTS
 1380 .E
 1390 LDX#7
 1400 .vodka
 1410 LDAspirp,X:BMItonic:STXaloof
 1420 LDAspirf,X:STAparas
 1430 LDAspirx,X:STAtempa
 1440 LDAspirc,X:STAtempx
 1450 LDAspiry,X:STAtempy
 1460 LDAspird,X:STAtempz
 1470 JSROPENS:BCSwater:JSRPLACE
 1480 .water LDXaloof
 1490 .tonic DEX:BPLvodka:RTS
 1500 .D
 1510 LDA#19:JSROSB
 1520 JSRMONST
 1530 LDX#3
 1540 .jiger
 1550 STXaloof
 1560 LDAanoth,X:BMInegat:BNEtiger
 1570 LDAbornm,X:BEQalive
 1580 DECbornm,X:BNEtiger
 1590 .alive
 1600 LDAmonsc,X:BNEammun
 1610 LDArandy:AND#1
 1620 BNEslide:TAY
 1630 LDAreptx:CMPxcoor,X:BCSchock
 1640 INY:BNEchock
 1650 .slide
 1660 LDY#3
 1670 LDArepty:CMPycoor,X:BCSchock
 1680 DEY
 1690 .chock
 1700 STYdirec,X
 1710 LDAycoor,X:CLC:ADCyadin,Y:PHA
 1720 LDAxcoor,X:CLC:ADCxadin,Y
 1730 TAX:PLA:JSRGBYTE:LDXaloof
 1740 CMP#2:BEQammun
 1750 CMP#3:BEQammun
 1760 CMP#6:BEQammun
 1770 CMP#30:BNEtiger
 1780 INCdeath
 1790 .ammun
 1800 INCmonsc,X:LDAmonsc,X
 1810 AND#3:STAmonsc,X:BNEtiger
 1820 JSRADDEM
 1830 .tiger
 1840 JSRSTEIN
 1850 DECmonsf,X:BNEnegat
 1860 LDA#3:STAmonsf,X
 1870 LDAframc,X:EOR#3:STAframc,X
 1880 .negat
 1890 DEX:BPLjiger
 1900 JSRMONST
 1910 JSRSPIRT
 1920 LDX#7
 1930 .wines
 1940 LDAspirp,X:BMIbeers
 1950 STXaloof
 1960 LDAreptx:CMPspirx,X:BNEenter
 1970 LDArepty:CMPspiry,X:BNEenter
 1980 INCdeath
 1990 .enter
 2000 LDAspirc,X:BNEflash
 2010 JSRSIDES
 2020 JSRFRONT
 2030 JSRFRONT
 2040 .flash
 2050 DECcount,X:BNEnofla
 2060 LDA#3:STAcount,X
 2070 LDAspirf,X:EOR#63:STAspirf,X
 2080 .nofla
 2090 INCspirc,X:LDAspirc,X:AND#3
 2100 STAspirc,X:BNEbeers
 2110 LDYspird,X
 2120 LDAspirx,X:CLC:ADCxadin,Y
 2130 STAspirx,X
 2140 LDAspiry,X:CLC:ADCyadin,Y
 2150 STAspiry,X
 2160 .beers
 2170 DEX:BPLwines
 2180 JMPSPIRT
 2190 .xadin
 2200 EQUB1:EQUB&FF
 2210 .yadin
 2220 EQUB0:EQUB0:EQUB&FF:EQUB1
 2230 .xdash
 2240 EQUB0:EQUB0
 2250 .ydash
 2260 EQUB&FF:EQUB1:EQUB0:EQUB0
 2270 .sides
 2280 EQUB2:EQUB3:EQUB1:EQUB0
 2290 .SIDES
 2300 LDAspirp,X:BMIsiout
 2310 LDYspird,X
 2320 LDAspiry,X:CLC:ADCydash,Y
 2330 STApages+1
 2340 LDAspirx,X:CLC:ADCxdash,Y
 2350 STApages
 2360 TAX:LDApages+1:JSRGBYTE:LDXaloof
 2370 CMP#2:BEQforwd
 2380 CMP#3:BEQforwd
 2390 CMP#6:BEQforwd
 2400 CMP#23:BEQcages
 2410 CMP#30:BNEsiout
 2420 .deded
 2430 INCdeath:RTS
 2440 .forwd
 2450 LDYspird,X:LDAsides,Y:STAspird,X
 2460 .siout RTS
 2470 .cages
 2480 DECspirp,X
 2490 LDXpages:LDYpages+1
 2500 LDA#1:STAparas:JSRWBYTE
 2510 LDApages:STAtempa
 2520 LDApages+1:STAtempy
 2530 LDA#0:STAtempx:STAtempz
 2540 JSROPENS:BCSpling
 2550 JSRBLOBS
 2560 .pling
 2570 LDA#3:JMPNOISE
 2580 .holly PLA:PLA:JMPbeers
 2590 .FRONT
 2600 LDAspirp,X:BMIfront
 2610 LDYspird,X
 2620 LDAspiry,X:CLC:ADCyadin,Y
 2630 STApages+1
 2640 LDAspirx,X:CLC:ADCxadin,Y
 2650 STApages
 2660 TAX:LDApages+1:JSRGBYTE:LDXaloof
 2670 CMP#2:BEQfront
 2680 CMP#3:BEQfront
 2690 CMP#6:BEQfront
 2700 CMP#23:BEQcages
 2710 CMP#30:BNEchang:BEQdeded
 2720 .chang
 2730 LDYspird,X:LDAinsid,Y:STAspird,X
 2740 .front RTS
 2750 .STEIN
 2760 LDAreptx:EORxcoor,X:BNEspots
 2770 LDArepty:EORycoor,X:BNEspots
 2780 INCdeath
 2790 .spots
 2800 LDYycoor,X:LDAxcoor,X
 2810 TAX:TYA:JSRGBYTE
 2820 LDX#2
 2830 .steel
 2840 CMPhamer,X:BEQkilit
 2850 DEX:BPLsteel
 2860 BMIstink
 2870 .kilit
 2880 LDXaloof
 2890 DECanoth,X
 2900 LDA#5:JSRNOISE
 2910 DECmonst
 2920 LDAscore:CLC:ADC#20:STAscore
 2930 BCCstink:INCscore+1
 2940 .stink
 2950 LDXaloof:RTS
 2960 .ADDEM
 2970 LDYdirec,X
 2980 LDAxcoor,X:CLC:ADCxadin,Y
 2990 STAxcoor,X
 3000 LDAycoor,X:CLC:ADCyadin,Y
 3010 STAycoor,X:RTS
 3020 .notes
 3030 EQUB96
 3040 \\:EQUB10
 3050 EQUB108
 3060 \\:EQUB5
 3070 EQUB96
 3080 \\:EQUB10
 3090 EQUB96
 3100 \\:EQUB10
 3110 EQUB108
 3120 \\:EQUB5
 3130 EQUB96
 3140 \\:EQUB10
 3150 EQUB100
 3160 \\:EQUB5
 3170 EQUB108
 3180 \\:EQUB5
 3190 EQUB100
 3200 \\:EQUB5
 3210 EQUB88
 3220 \\:EQUB10
 3230 EQUB96
 3240 \\:EQUB5
 3250 EQUB100
 3260 \\:EQUB5
 3270 EQUB96
 3280 \\:EQUB5
 3290 EQUB80
 3300 \\:EQUB10
 3310 .F
 3320 LDA#((F-notes)/2)-1:STAtempa
 3330 .jingl
 3340 LDA#128:LDX#&FA:JSROSB
 3350 CPX#4:BCCjingl
 3360 LDA#((F-notes)/2)-1:SBCtempa
 3370 ASLA:TAX
 3380 LDAnotes,X:ADC#24:STAmelod+4
 3390 LDAnotes+1,X:LSRA:STAmelod+6
 3400 LDX#melod AND255:LDY#melod/256
 3410 LDAtunes:BNEbuttr
 3420 LDA#7:JSROSO
 3430 .buttr
 3440 DECtempa:BPLjingl:RTS
 3450 .melod
 3460 EQUW1:EQUW1:EQUW0:EQUW0
 3470 .BARS1
 3480 EQUBc ORoct4:EQUB&FF
 3490 EQUBe ORoct4:EQUBd ORoct4
 3500 EQUBe ORoct4:EQUBd ORoct4
 3510 EQUBc ORoct4:EQUB&FF
 3520 EQUB&FF:EQUB&FF
 3530 EQUBe ORoct4:EQUBd ORoct4
 3540 EQUBe ORoct4:EQUBf ORoct4
 3550 EQUBg ORoct4:EQUB&FF
 3560 \
 3570 EQUBg ORoct4:EQUB&FF
 3580 EQUBc ORoct2:EQUB&FF
 3590 EQUBc ORoct3:EQUB&FF
 3600 EQUBg ORoct2:EQUB&FF
 3610 EQUBc ORoct3:EQUB&FF
 3620 EQUBc ORoct2:EQUB&FF
 3630 EQUBc ORoct3:EQUB&FF
 3640 EQUBg ORoct2:EQUB&FF
 3650 EQUBc ORoct3:EQUB&FF
 3660 .BARS2
 3670 EQUB&FF:EQUB&FF
 3680 EQUBf` ORoct4:EQUBg ORoct4
 3690 EQUBa ORoct4:EQUBg ORoct4
 3700 EQUBf` ORoct4:EQUBg ORoct4
 3710 EQUBa ORoct4:EQUB&FF
 3720 EQUB&FF:EQUB&FF
 3730 EQUB&FF:EQUB&FF
 3740 EQUB&FF:EQUB&FF
 3750 \
 3760 EQUBd OR oct2:EQUB&FF
 3770 EQUBd OR oct3:EQUB&FF
 3780 EQUBa OR oct2:EQUB&FF
 3790 EQUBd OR oct3:EQUB&FF
 3800 EQUBd OR oct2:EQUB&FF
 3810 EQUBc OR oct3:EQUB&FF
 3820 EQUBb OR oct2:EQUB&FF
 3830 EQUBa OR oct2:EQUB&FF
 3840 .BARS3
 3850 EQUB&FF:EQUB&FF
 3860 EQUBb ORoct4:EQUBa ORoct4
 3870 EQUBg ORoct4:EQUBa ORoct4
 3880 EQUBg ORoct4:EQUBf ORoct4
 3890 EQUBe ORoct4:EQUBf ORoct4
 3900 EQUBe ORoct4:EQUBd ORoct4
 3910 EQUBe ORoct4:EQUBd ORoct4
 3920 EQUBc ORoct4:EQUBd ORoct4
 3930 \
 3940 EQUBg ORoct2:EQUB&FF
 3950 EQUBg ORoct3:EQUB&FF
 3960 EQUBd ORoct2:EQUB&FF
 3970 EQUBg ORoct3:EQUB&FF
 3980 EQUBg ORoct2:EQUB&FF
 3990 EQUBg ORoct3:EQUB&FF
 4000 EQUBd ORoct2:EQUB&FF
 4010 EQUBg ORoct3:EQUB&FF
 4020 .BARS4
 4030 EQUBg ORoct3:EQUBa ORoct3
 4040 EQUBc ORoct4:EQUBd ORoct4
 4050 EQUBe ORoct4:EQUBd ORoct4
 4060 EQUBc ORoct4:EQUBa ORoct3
 4070 EQUBc ORoct4:EQUB&FF
 4080 EQUB&FF:EQUB&FF
 4090 EQUB&FF:EQUB&FF
 4100 EQUB&FF:EQUB&FF
 4110 \
 4120 EQUBc ORoct2:EQUB&FF
 4130 EQUBc ORoct3:EQUB&FF
 4140 EQUBg ORoct2:EQUB&FF
 4150 EQUBc ORoct3:EQUB&FF
 4160 EQUBc ORoct2:EQUB&FF
 4170 EQUBg ORoct2:EQUB&FF
 4180 EQUBa ORoct2:EQUB&FF
 4190 EQUBb ORoct2:EQUB&FF
 4200 .BARS7
 4210 EQUBg ORoct4:EQUBb ORoct4
 4220 EQUBf ORoct4:EQUBa ORoct4
 4230 EQUBe ORoct4:EQUBg ORoct4
 4240 EQUBd ORoct4:EQUBf ORoct4
 4250 EQUBc ORoct4:EQUBe ORoct4
 4260 EQUBb ORoct3:EQUBd ORoct4
 4270 EQUBa ORoct3:EQUBc ORoct4
 4280 EQUBg ORoct3:EQUBb ORoct3
 4290 \
 4300 EQUBg ORoct2:EQUB&FF
 4310 EQUBg ORoct3:EQUB&FF
 4320 EQUBd ORoct2:EQUBe ORoct2
 4330 EQUBf ORoct2:EQUBg ORoct2
 4340 EQUBf ORoct2:EQUB&FF
 4350 EQUBf ORoct3:EQUB&FF
 4360 EQUBd ORoct2:EQUBe ORoct2
 4370 EQUBf ORoct2:EQUBd ORoct2
 4380 .BARS8
 4390 EQUBe ORoct4:EQUBd ORoct4
 4400 EQUBc ORoct4:EQUBd ORoct4
 4410 EQUBg ORoct3:EQUBa ORoct3
 4420 EQUBc ORoct4:EQUBd ORoct4
 4430 EQUBc ORoct4:EQUB&FF
 4440 EQUBb ORoct3:EQUBc ORoct4
 4450 EQUBd ORoct4:EQUBb ORoct3
 4460 EQUBa ORoct3:EQUBg ORoct3
 4470 \
 4480 EQUBe ORoct2:EQUB&FF
 4490 EQUBe ORoct3:EQUB&FF
 4500 EQUBd ORoct2:EQUB&FF
 4510 EQUBd ORoct3:EQUB&FF
 4520 EQUBc ORoct2:EQUB&FF
 4530 EQUBg ORoct2:EQUBa ORoct2
 4540 EQUBb ORoct2:EQUBg ORoct2
 4550 EQUBf ORoct2:EQUBe ORoct2
 4560 .hamer
 4570 EQUB0:EQUB24:EQUB26
 4580 .insid
 4590 EQUB3:EQUB2:EQUB0:EQUB1
 4600 ]:NEXT

